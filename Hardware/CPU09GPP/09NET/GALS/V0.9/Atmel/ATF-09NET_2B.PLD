/** WinCUPL Design Description **/

Name       09NET_G2 U8;
Partno     ATF22V10C;
Date       20/06/2025;
Revision   02;
Designer   CdeJ;
Company    09NET;
Assembly   U8;
Location   _09NET_GAL2;
Device     g22v10;

/* ------------- PIN Declarations ---------- */

PIN  3    = U7_IOSEL;
PIN  4    = Q_FF1;

PIN  6    = C_E;
PIN  7    = C_RW_;
PIN  8    = C_A0;
PIN  9    = C_A1;
PIN  10   = C_A2;
PIN  11   = AUTO_EN;

PIN  14   = CLK_574;     
PIN  15   = EN_STAT_;
PIN  16   = CLK_FF0;
PIN  17   = CLK_8_16;
PIN  18   = S0H;
PIN  19   = S1L;
PIN  20   = SOEL_;
PIN  21   = SOEH_;
PIN  22   = S1H;
PIN  23   = S0L;

/* --------- Boolean Equation Segment ------ */

/* read status */
!EN_STAT_ = !C_A0 & !C_A1 & !C_A2 & C_E & C_RW_ & !U7_IOSEL ; /* XXX0 [R] */

/* write control latch */
CLK_574 = !U7_IOSEL & !C_A2 & !C_A1 & !C_A0 & C_E & !C_RW_ ; /* XXX0 [W] */

/* read HIBYTE SR */
!SOEH_ = !C_A0 & C_A2 & C_E & C_RW_ & !U7_IOSEL ;         /* XXX4 [R]  XXX6 [R] non auto advance */

/* read LOBYTE SR */
!SOEL_ = C_A0 & C_A2 & C_E & C_RW_ & !U7_IOSEL ;           /* XXX5 [R]  XXX7 [R] non auto afvance */

/* write HIBYTE SR */
S1H = !C_A0 & C_A2 & C_E & !C_RW_ & !U7_IOSEL  ;          /* XXX4 [W] XXX6 [W] non auto advance */
S0H = S1H 
      # Q_FF1 ;
 
/* write LOBYTE SR */
S1L = C_A0 & C_A2 & C_E & !C_RW_ & !U7_IOSEL ;           /* XXX5 [W] XXX7 [W] non auto advance */
S0L = S1L 
      # Q_FF1;

/* whether the shift advances one byte position */
!CLK_FF0 = !C_A1 & C_A2 & !C_RW_ & !U7_IOSEL
           # AUTO_EN & !C_A1 & C_A2 & !U7_IOSEL ;  /* write or read/write */

/* any R/W to these locations do shifts */
CLK_8_16 = !C_A1 & C_A2 & C_E & !U7_IOSEL ;               /* XXX4/XXX5 */
