/** WinCUPL Design Description **/

Name       CPUXXCMI  U13  GAL1;
Partno     ATF22V10C;
Date       06/02/2024;
Revision   41;
Designer   CdeJ;
Company    CPU09;
Assembly   _CXXCMI_G1 3MHz;
Location   U13;
Device     g22v10;

/* ----------- PIN Declarations ----------- */
PIN  1    = C_W_;     /* IN */
PIN  2    = C_E;      /* IN */
PIN  3    = C_A15;    /* IN */
PIN  4    = C_A14;    /* IN */
PIN  5    = C_A13;    /* IN */
PIN  6    = C_A12;    /* IN */
PIN  7    = C_A11;    /* IN */
PIN  8    = C_A10;    /* IN */
PIN  9    = VMA;      /* IN */
PIN 10    = SYSE;     /* IN */
PIN 11    = BLK_;     /* IN */

PIN 13    = EIODB;    /* IN EXTERN IO */  
PIN 14    = BA;       /* IN */
PIN 15    = DBEN_;    /* OUT DB enable */
PIN 16    = U9_CE_;   /* OUT RAM */
PIN 17    = BS;       /* IN */
PIN 18    = SEL_;     /* OUT reset SYS */
PIN 19    = C_Q;     /* IN */
PIN 20    = IOSEL_ ;  /* OUT ALL IO */ 
PIN 21    = IACK;     /* OUT */
PIN 22    = U8_CE_;   /* OUT ROM */
PIN 23    = DBDIR;    /* OUT BUFFER */

/* -------- Boolean Equation Segment --------- */


/*  Decode primary memory space */

/* System ROM, read, enabled on F800-FFFF */
!U8_CE_ = C_A11 & C_A12 & C_A13 & C_A14 & C_A15 & C_E & C_W_ & VMA
          # C_A11 & C_A12 & C_A13 & C_A14 & C_A15 & C_Q & C_W_ & VMA; /* F800-FFFF */


/* RAM   0000-EFFF,F400-F7FF  (65K) minus ROM/IO, unless /BLK_ */
U9_CE_ = !C_E & !C_Q # !BLK_ & !C_A15 # !BLK_ & !C_A14 # !BLK_ & !C_A13 # !BLK_ & !C_A12
         # C_A11 & C_A12 & C_A13 & C_A14 & C_A15 # !BLK_ & C_A11 
         # !C_A10 & C_A12 & C_A13 & C_A14 & C_A15 # !BLK_ & !C_A10;

/* Where databuffer direction is selected */
!DBDIR = BLK_ & !EIODB # !C_W_;

/* IO part  F000-F3FF */
IOSEL_ = C_A10 # C_A11 # !C_A12 # !C_A13 # !C_A14 # !C_A15 # !VMA; /* responds to F000-F3FF */

/* VMA */
/* /OVMA_ = VMA   ; VMA VALID -> /OVMA_ ON U3B PIN 6 */

/* DATA BUS driver enable */
DBEN_ = BA # BLK_ & !EIODB;  /* EXTERN IO or EXTRNAL DECODING */

/* IACK cycles, enter supervisor */
!IACK =  BA # !BS; /* IACK */

/* Write to ROM, exit supervisor */
SEL_ = !C_A11 # !C_A12 # !C_A13 # !C_A14 # !C_A15 # !C_E # C_W_ # !VMA; /* WRITE to ROM */

